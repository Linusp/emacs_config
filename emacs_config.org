#+TITLE: Emacs 配置

* 一些待考虑进行安装的插件

  - symbol-overlay: 能高亮自己定制的关键词，感觉可以替代 hl-todo
  - git-gutter+: 说是支持了 git-gutter 暂时还没支持的一些 feature
  - key-chord: 似乎能把固定的按键序列映射为 emacs 函数/命令的样子，没有实际用过
  - hydra: abo-abo 写的另外一个插件，用来简化快捷键序列
  - pdf-tools: 懒猫写的 PDF 阅读插件
  - ob-async: 能让 org babel 的代码异步执行的插件，用过，但还没真正用起来

* 初始化设置

  启动 server 模式，为后面的 edit-server 以及终端使用 emacsclient 做准备
  #+BEGIN_SRC emacs-lisp
  (server-start)
  #+END_SRC

  临时文件设置
  #+BEGIN_SRC emacs-lisp
  (setq make-backup-files nil)
  (setq backup-inhibited t)               ;不产生备份
  (setq auto-save-list-file-prefix nil)
  (setq auto-save-default nil)            ;不生成 #fname# 格式的临时文件
  #+END_SRC

  剪贴板设置
  #+BEGIN_SRC emacs-lisp
  (setq x-select-enable-clipboard t)      ;共享剪贴板
  (setq x-select-enable-primary t)        ;共享中键复制内容
  (setq kill-ring-max 200)                ;killring 大小
  #+END_SRC

  滚动设置
  #+BEGIN_SRC emacs-lisp
  (setq scroll-conservatively 101)        ;光标移出时平滑滚动而不是重定位到中央
  (setq mouse-wheel-scroll-amount '(1))   ;用鼠标滚动时一次只滚动一行
  #+END_SRC

  设置打开网页链接时使用的默认浏览器为 Chrome
  #+BEGIN_SRC emacs-lisp
  (setq browse-url-generic-program (executable-find "google-chrome"))
  (setq browse-url-browser-function 'browse-url-generic)
  #+END_SRC

  其他杂项
  #+BEGIN_SRC emacs-lisp
  (setq default-major-mode 'text-mode)    ;将缺省模式设置为text模式

  (setq visible-bell nil)                 ;关闭错误操作时的窗口闪动警告
  (setq make-pointer-invisible t)         ;输入时隐藏鼠标指针

  (setq auto-image-file-mode t)           ;让emacs可以直接打开和显示图片

  (setq vc-handled-backends '(Git SVN))   ;打开版本控制

  (fset 'yes-or-no-p 'y-or-n-p)           ;用 y/n 替代 yes/no
  (global-visual-line-mode 0)
  #+END_SRC

* 扩展安装和加载

  定义一个变量，用来保存依赖的 packge 列表
  #+BEGIN_SRC emacs-lisp
  (setq my-package-list '())
  #+END_SRC

  这些是和界面、外观相关的一些插件
  #+BEGIN_SRC emacs-lisp
  (add-to-list 'my-package-list '(nlinum nlinum) t)
  (add-to-list 'my-package-list '(color-theme color-theme) t)
  (add-to-list 'my-package-list '(color-theme-modern color-theme-modern) t)
  (add-to-list 'my-package-list '(hydandata-light-theme hydandata-light-theme) t)
  (add-to-list 'my-package-list '(color-theme-sanityinc-tomorrow color-theme-sanityinc-tomorrow) t)
  #+END_SRC

  各种 major-mode，包含一些编程语言的支持
  #+BEGIN_SRC emacs-lisp
  (add-to-list 'my-package-list '(graphviz-dot-mode graphviz-dot-mode) t)
  (add-to-list 'my-package-list '(js2-mode js2-mode) t)
  (add-to-list 'my-package-list '(markdown-mode markdown-mode) t)
  (add-to-list 'my-package-list '(slime slime) t)
  (add-to-list 'my-package-list '(flycheck flycheck) t)
  (add-to-list 'my-package-list '(cypher-mode cypher-mode) t)
  (add-to-list 'my-package-list '(plantuml-mode plantuml-mode) t)
  (add-to-list 'my-package-list '(restclient restclient) t)
  #+END_SRC

  org-mode 相关
  #+BEGIN_SRC emacs-lisp
  (add-to-list 'my-package-list '(ob org-plus-contrib) t)
  (add-to-list 'my-package-list '(ob-ipython ob-ipython) t)
  (add-to-list 'my-package-list '(ob-cypher ob-cypher) t)
  (add-to-list 'my-package-list '(ob-restclient ob-restclient) t)
  (add-to-list 'my-package-list '(anki-editor anki-editor) t)
  #+END_SRC

  下面的则是一些效率相关的插件
  #+BEGIN_SRC emacs-lisp
  (add-to-list 'my-package-list '(ag ag) t)
  (add-to-list 'my-package-list '(counsel counsel) t)
  (add-to-list 'my-package-list '(avy avy) t)
  (add-to-list 'my-package-list '(expand-region expand-region) t)
  (add-to-list 'my-package-list '(helm-utils helm) t)
  (add-to-list 'my-package-list '(helm helm-core) t)
  (add-to-list 'my-package-list '(swiper ivy) t)
  (add-to-list 'my-package-list '(magit magit) t)
  (add-to-list 'my-package-list '(py-autopep8 py-autopep8) t)
  (add-to-list 'my-package-list '(yafolding yafolding) t)
  #+END_SRC

  自动补全，使用 yasnippet 和 company，因为不用 auto-complete 这个包，就不用安装 jedi 了，company-jedi 会附带安装上 jedi-core，这就够了，jedi 这个包是使用 auto-complete 的。
  #+BEGIN_SRC emacs-lisp
  (add-to-list 'my-package-list '(yasnippet yasnippet) t)
  (add-to-list 'my-package-list '(company company) t)
  (add-to-list 'my-package-list '(jedi-core jedi-core) t)
  (add-to-list 'my-package-list '(company-jedi company-jedi) t)
  #+END_SRC

  auto-virtualenvwrapper，可以根据打开的 python 文件，找到对应的 venv，然后激活这个 venv，用的是 virtualenvwrapper 这个包，所以把这个也加上
  #+BEGIN_SRC emacs-lisp
  (add-to-list 'load-path "~/.emacs.d/site-lisp/virtualenvwrapper.el/")
  (require 'virtualenvwrapper)

  (add-to-list 'load-path "~/.emacs.d/site-lisp/auto-virtualenvwrapper.el/")
  (require 'auto-virtualenvwrapper)
  #+END_SRC

  edit-server
  #+BEGIN_SRC emacs-lisp
  (add-to-list 'my-package-list '(edit-server edit-server) t)
  #+END_SRC

  检查插件，如果无法找到则进行安装
  #+BEGIN_SRC emacs-lisp
    (dolist (pkg-info my-package-list)
      (when (not (require (nth 0 pkg-info) nil :noerror))
        (progn
        (message "install %s now..." (symbol-name (nth 1 pkg-info)))
        (setq url-http-attempt-keepalives nil)
        (package-refresh-contents)
        (package-install (nth 1 pkg-info)))))
  #+END_SRC

  载入一些内建的功能，以及系统外插件
  #+BEGIN_SRC emacs-lisp
  (require 'font-lock)
  (require 'electric)
  (load-library "hideshow")

  (add-to-list 'load-path "/usr/share/emacs/site-lisp/ccrypt/")
  (require 'ps-ccrypt)                    ;from `apt-get install ccrypt`

  (add-to-list 'load-path "/usr/share/emacs/site-lisp/global/")
  (require 'gtags)                        ;from `apt-get install global`

  (add-to-list 'load-path "~/.emacs.d/site-lisp/org-brain/")
  (require 'org-brain)

  (add-to-list 'load-path "~/.emacs.d/site-lisp/moody/")
  (require 'moody)

  (add-to-list 'load-path "~/.emacs.d/site-lisp/minions/")
  (require 'minions)

  (add-to-list 'load-path "~/.emacs.d/site-lisp/smex/")
  (require 'smex)

  (add-to-list 'load-path "~/.emacs.d/site-lisp/helm-org-rifle/")
  (require 'helm-org-rifle)

  (add-to-list 'load-path "~/.emacs.d/site-lisp/emacs-helm-ag/")
  (require 'helm-ag)

  (add-to-list 'load-path "~/.emacs.d/site-lisp/helm-swoop/")
  (require 'helm-swoop)

  (add-to-list 'load-path "~/.emacs.d/site-lisp/highlight-parentheses.el/")
  (require 'highlight-parentheses)

  (add-to-list 'load-path "~/.emacs.d/site-lisp/hl-todo/")
  (require 'hl-todo)

  (add-to-list 'load-path "~/.emacs.d/site-lisp/pretty-symbols")
  (require 'pretty-symbols)

  (add-to-list 'load-path "~/.emacs.d/site-lisp/emacs-htmlize")
  (require 'htmlize)

  (add-to-list 'load-path "~/.emacs.d/site-lisp/git-gutter")
  (require 'git-gutter)

  (add-to-list 'load-path "~/.emacs.d/site-lisp/org-bullets")
  (require 'org-bullets)

  (add-to-list 'load-path "~/.emacs.d/site-lisp/telega.el") ;依赖 svg
  (require 'telega)
  (setq telega-proxies
        (list
         '(:server "localhost" :port 1235 :enable t :type (:@type "proxyTypeHttp"))
         '(:server "localhost" :port 1080 :enable t :type (:@type "proxyTypeSocks5"))
         ))
  #+END_SRC

* 外观设置

** 字体

   #+BEGIN_SRC emacs-lisp
   (set-language-environment 'UTF-8)
   (set-locale-environment "UTF-8")
   ;; Emacs 23.1 set-default-font -> set-frame-font
   (set-frame-font "Dejavu Sans Mono 12" nil t)
   (set-fontset-font "fontset-default" 'unicode "WenQuanYi Micro Hei Mono 15");
   (setq face-font-rescale-alist '(
                                   ("WenQuanYi Micro Hei Mono" . 1.3)
                                   ))
   #+END_SRC

** 界面

   基本界面定制
   #+BEGIN_SRC emacs-lisp
   (setq initial-scratch-message "")               ;将 scratch 中的内容置空
   (setq inhibit-startup-message t)                ;关闭启动画面
   (setq frame-title-format "[%b]")                ;显示buffer的名字
   (setq user-full-name "Linusp")                  ;用户名
   (setq user-mail-address "linusp1024@gmail.com") ;用户邮箱

   (column-number-mode 0)                  ;显示列号
   (tool-bar-mode 0)                       ;关闭工具栏
   (menu-bar-mode 0)                       ;关闭菜单栏
   (scroll-bar-mode 0)                     ;关闭滚动条
   (blink-cursor-mode 0)                   ;关闭光标闪烁
   (global-nlinum-mode 0)                  ;显示行号
   (column-number-mode 1)                  ;显示列号
   (modify-all-frames-parameters           ;使用更好看的光标
    (list (cons 'cursor-type 'bar)))
   (global-hl-line-mode 1)                 ;开启当前行高亮
   (set-face-attribute 'hl-line nil :background "gray5")
   (global-git-gutter-mode 1)              ;开启 git-gutter
   #+END_SRC

   空白符显示设置，主要是把制表符啊、多余的空格啊这种高亮一下
   #+BEGIN_SRC emacs-lisp
   (setq whitespace-style '(face tabs trailing tab-mark)) ;高亮制表符、结尾冗余空格
   (setq whitespace-display-mappings
         '((space-mark 32 [32] [183] [46])
           (space-mark 160 [164] [95])
           (space-mark 2208 [2212] [95])
           (space-mark 2336 [2340] [95])
           (space-mark 3616 [3620] [95])
           (space-mark 3872 [3876] [95])
           (newline-mark 10 [182 10] [36 10])
           (tab-mark 9 [187 9] [92 9])))
   (global-whitespace-mode t)
   #+END_SRC

   加载自定义 color-theme
   #+BEGIN_SRC emacs-lisp
   (add-to-list 'load-path "~/.emacs.d/themes/")
   (require 'color-theme-tomorrow)
   (require 'color-theme-dust)
   (require 'color-theme-ada)
   (require 'color-theme-smoothy)
   (require 'color-theme-mossysparks)
   #+END_SRC

   主题设置，包含 color-theme 和 mode-line 的美化
   #+BEGIN_SRC emacs-lisp
   (load-theme 'sanityinc-tomorrow-eighties t)
   #+END_SRC

   然后默认将 Emacs 最大化
   #+BEGIN_SRC emacs-lisp
   (toggle-frame-maximized)
   #+END_SRC

   启用 moody modeline 和 minions
   #+BEGIN_SRC emacs-lisp
   (minions-mode 1)
   (setq x-underline-at-descent-line t)
   (setq moody-mode-line-height 24)
   (moody-replace-mode-line-buffer-identification)
   (moody-replace-vc-mode)
   #+END_SRC

* 功能增强

  #+BEGIN_SRC emacs-lisp
  (smex-initialize)
  (global-set-key (kbd "M-x") 'smex)
  (global-set-key (kbd "M-X") 'smex-major-mode-commands)
  (ido-mode t)
  (setq windmove-wrap-around t)
  #+END_SRC

* 编程设置

** 杂项

   #+BEGIN_SRC emacs-lisp
   (electric-pair-mode t)                                   ;开启自动括号补全
   (electric-indent-mode t)                                 ;开启智能缩进
   (electric-layout-mode 0)                                 ;关闭智能自动换行
   (global-font-lock-mode 1)                                ;开启全局语法高亮
   (setq default-tab-width 4)
   (setq-default indent-tabs-mode nil)
   (add-hook 'prog-mode-hook 'hl-todo-mode)                 ;高亮 TODO 等单词
   (add-hook 'prog-mode-hook 'pretty-symbols-mode)          ;显示 Unicode 字符
   (dolist (command '(yank yank-pop))
     (eval
      `(defadvice ,command (after indent-region activate)
         (and (not current-prefix-arg)
              (member major-mode
                      '(emacs-lisp-mode
            lisp-mode
            scheme-mode
            python-mode
            c-mode
            c++-mode
            latex-mode
            plain-tex-mode))
              (let ((mark-even-if-inactive
                     transient-mark-mode))
        (indent-region (region-beginning)
                               (region-end) nil))))))
   (add-hook 'after-init-hook 'global-company-mode) ;使用 company-mode 来进行补全
   (add-to-list 'company-backends 'company-yasnippet)
   #+END_SRC

** 模板和自动补全

   #+BEGIN_SRC emacs-lisp
   (yas-global-mode 1)
   (yas-minor-mode-on)
   (define-key yas-minor-mode-map [(tab)] nil)
   (define-key yas-minor-mode-map (kbd "TAB") nil)
   (define-key yas-minor-mode-map (kbd "C-;") 'yas-expand)
   #+END_SRC

   company 的设置
   #+BEGIN_SRC emacs-lisp
   (setq compandy-minimum-prefix-length 1)
   (setq company-tooltip-align-annotations t)
   (setq company-transformers '(company-sort-by-occurrence))
   (setq company-selection-wrap-around t)
   (setq company-tooltip-limit 10)
   (define-key company-active-map (kbd "M-n") nil)
   (define-key company-active-map (kbd "M-p") nil)
   (define-key company-active-map (kbd "C-n") 'company-select-next)
   (define-key company-active-map (kbd "C-p") 'company-select-previous)
   (define-key company-active-map (kbd "TAB") 'company-complete-common-or-cycle)
   (define-key company-active-map (kbd "<tab>") 'company-complete-common-or-cycle)
   (define-key company-active-map (kbd "S-TAB") 'company-select-previous)
   (define-key company-active-map (kbd "<backtab>") 'company-select-previous)
   #+END_SRC

** C/C++

   #+BEGIN_SRC emacs-lisp
   (c-add-style "linusp"
                '((c-basic-offset . 4)
                  (c-comment-only-line-offset . 0)
                  (c-hanging-braces-alist
                   (brace-list-open)
                   (brace-entry-open)
                   (substatement-open after)
                   (block-close . c-snug-do-while)
                   (arglist-cont-nonempty))
                  (c-cleanup-list brace-else-brace)
                  (c-offsets-alist
                   (case-label . +)
                   (statement-block-intro . +)
                   (knr-argdecl-intro . 0)
                   (substatement-open . 0)
                   (substatement-label . 0)
                   (label . 0)
                   (statement-cont . +))))
   (defun gtags-root-dir ()
    "Returns GTAGS root directory or nil if doesn't exist."
    (with-temp-buffer
      (if (zerop (call-process "global" nil t nil "-pr"))
          (buffer-substring (point-min) (1- (point-max)))
        nil)))
   (setq c-default-style "linusp")
   (defun my-cc-mode-config ()
     (setq c-toggle-auto-state t
           c-basic-offset      4          ;缩进宽度为4
           default-tab-width   4          ;制表符宽度为4
           indent-tabs-mode    nil        ;不使用tab键缩进
           )
     (linum-mode t)
     (hs-minor-mode t)
     (whitespace-mode t)
     (gtags-mode t)
     (hl-line-mode t)
     ;; (hidden-minor-mode)
     (highlight-parentheses-mode t))
   (add-hook 'c-mode-hook 'my-cc-mode-config)
   (add-hook 'c++-mode-hook 'my-cc-mode-config)
   #+END_SRC

** Python

   jedi 的安装，首先参照 [[https://archive.zhimingwang.org/blog/2015-04-26-using-python-3-with-emacs-jedi.html][这篇文章]] 设置 python3 的 jedi
   #+BEGIN_SRC shell
   mkdir -p ~/.emacs.d/.python-environments
   virtualenv -p /usr/bin/python3  --prompt="<venv:jedi>" jedi
   pip install --upgrade ~/.emacs.d/elpa/jedi-core-20170319.2107/
   #+END_SRC

   然后设置 jedi，需要注意的是，这里的 "jedi:server-command" 需要设置一下
   #+BEGIN_SRC emacs-lisp
   (add-hook 'python-mode-hook 'jedi:setup)
   (setq jedi:complete-on-dot t)
   (setq jedi:environment-root "jedi")
   (setq jedi:server-command (jedi:-env-server-command))
   (setq jedi:use-shortcuts t)             ;能使用 import tensorflow as tf 后的 tf 来补全
   (message "jedi:server-command is %S" jedi:server-command)
   #+END_SRC

   另外，遵照 [[https://github.com/tkf/emacs-jedi][emacs-jedi]] 项目里的说明，因为我使用 company-mode，就不需要安装 jedi，只需要安装 company-jedi 就好了， company-jedi 里会安装 jedi-core，这就够了。

   flycheck 的相关设置
   #+BEGIN_SRC emacs-lisp
   (require 'auto-virtualenvwrapper)
   (add-hook 'python-mode-hook #'auto-virtualenvwrapper-activate)
   (add-hook 'window-configuration-change-hook #'auto-virtualenvwrapper-activate)
   (add-hook 'focus-in-hook #'auto-virtualenvwrapper-activate)
   (setq auto-virtualenvwrapper-verbose nil)

   ;; 设置让 flycheck 使用 virtualenv 中的 pylint
   (declare-function python-shell-calculate-exec-path "python")

   (defun flycheck-virtualenv-executable-find (executable)
     "Find an EXECUTABLE in the current virtualenv if any."
     (if (bound-and-true-p python-shell-virtualenv-root)
         (let ((exec-path (python-shell-calculate-exec-path)))
           (executable-find executable))
       (executable-find executable)))

   (defun flycheck-virtualenv-setup ()
     "Setup Flycheck for the current virtualenv."
     (setq-local flycheck-executable-find #'flycheck-virtualenv-executable-find))
   #+END_SRC

   #+BEGIN_SRC emacs-lisp
   (defun my-python-mode-config ()
     (setq python-indent-offset 4
           python-indent 4
           indent-tabs-mode nil
           default-tab-width 4)
     (setenv "IPY_TEST_SIMPLE_PROMPT" "1") ;fixed emacs 25.1 bug
     (hs-minor-mode t)
     (auto-fill-mode 0)
     (whitespace-mode t)
     (hl-line-mode t)
     (pretty-symbols-mode t)
     (flycheck-mode t)
     (add-to-list 'company-backends 'company-jedi)
     (set (make-local-variable 'electric-indent-mode) nil))
   (add-to-list 'auto-mode-alist '("\\.py\\'" . python-mode))
   (add-to-list 'auto-mode-alist '("SConstruct" . python-mode))
   (setq python-shell-interpreter "ipython"
         python-shell-interpreter-args ""
         python-shell-prompt-regexp "In \\[[0-9]+\\]: "
         python-shell-prompt-output-regexp "Out\\[[0-9]+\\]: "
         python-shell-completion-setup-code "from IPython.core.completerlib import module_completion"
         python-shell-completion-module-string-code "';'.join(module_completion('''%s'''))\n"
         python-shell-completion-string-code "';'.join(get_ipython().Completer.all_completions('''%s'''))\n")
   (add-hook 'python-mode-hook 'my-python-mode-config)
   #+END_SRC

   设置 pep8
   #+BEGIN_SRC emacs-lisp
   (add-hook 'python-mode-hook 'py-autopep8-enable-on-save)
   (setq py-autopep8-options '("--max-line-length=100"))
   #+END_SRC

** Lisp

   #+BEGIN_SRC emacs-lisp
   (defun lisp-mode-config ()
     (highlight-parentheses-mode t)
     (hs-minor-mode t)
     (hl-line-mode t)
     (whitespace-mode t)
     (pretty-symbols-mode t)
     (set (make-local-variable 'electric-pair-mode) nil)
     )
   ;; My slime
   (defun my-slime ()
     (interactive)
     (slime)
     (delete-other-windows))
   ;; CL
   (setq slime-lisp-implementations '((sbcl ("sbcl"))))
   (setq inferior-lisp-program "sbcl")
   (slime-setup '(slime-fancy))
   (add-hook 'emacs-lisp-mode-hook 'lisp-mode-config)  ;Emacs Lisp
   (add-hook 'lisp-mode-hook       'lisp-mode-config)  ;Common Lisp
   (add-hook 'slime-repl-mode      'lisp-mode-config)  ;Slime REPL
   (add-hook 'inferior-octave-mode-hook
             (lambda ()
               (turn-on-font-lock)
               (define-key inferior-octave-mode-map [up]
                 'comint-previous-input)
               (define-key inferior-octave-mode-map [down]
                 'comint-next-input)))
   #+END_SRC

* Org-mode 设置

** 基础设置

   org-mode 的基本设置
   #+BEGIN_SRC emacs-lisp
   (add-to-list 'auto-mode-alist '("\\.txt\\'" . org-mode))
   (defun my-org-mode-config ()
     (setq org-edit-src-content-indentation 0
           org-src-tab-acts-natively t
           org-src-fontify-natively t
           org-confirm-babel-evaluate nil
           org-startup-with-inline-images t
           truncate-lines nil
           org-export-with-sub-superscripts '{}
           org-hide-emphasis-markers t
           org-image-actual-width nil
           org-completion-use-ido t
           org-html-checkbox-type 'html
           )
     (org-bullets-mode 1)
     (anki-editor-mode 1)
     )
   (add-hook 'org-mode-hook 'my-org-mode-config)
   (font-lock-add-keywords 'org-mode
                           '(("^ +\\([-*]\\) "
                              (0 (prog1 () (compose-region (match-beginning 1) (match-end 1) "•"))))))
   #+END_SRC

   设置默认的 org 文件
   #+BEGIN_SRC emacs-lisp
   (setq org-directory "~/Dropbox/org")
   (setq org-default-notes-file (concat org-directory "/inbox.org"))
   #+END_SRC

** babel 设置

   更好地显示 babel 的 source block，来自: [[https://pank.eu/blog/pretty-babel-src-blocks.html][pretty org babel blocks]]
   #+BEGIN_SRC emacs-lisp
   (with-eval-after-load 'org
     (defvar-local rasmus/org-at-src-begin -1
       "Variable that holds whether last position was a ")

     (defvar rasmus/ob-header-symbol ?☰
       "Symbol used for babel headers")

     (defun rasmus/org-prettify-src--update ()
       (let ((case-fold-search t)
             (re "^[ \t]*#\\+begin_src[ \t]+[^ \f\t\n\r\v]+[ \t]*")
             found)
         (save-excursion
           (goto-char (point-min))
           (while (re-search-forward re nil t)
             (goto-char (match-end 0))
             (let ((args (org-trim
                          (buffer-substring-no-properties (point)
                                                          (line-end-position)))))
               (when (org-string-nw-p args)
                 (let ((new-cell (cons args rasmus/ob-header-symbol)))
                   (cl-pushnew new-cell prettify-symbols-alist :test #'equal)
                   (cl-pushnew new-cell found :test #'equal)))))
           (setq prettify-symbols-alist
                 (cl-set-difference prettify-symbols-alist
                                    (cl-set-difference
                                     (cl-remove-if-not
                                      (lambda (elm)
                                        (eq (cdr elm) rasmus/ob-header-symbol))
                                      prettify-symbols-alist)
                                     found :test #'equal)))
           ;; Clean up old font-lock-keywords.
           (font-lock-remove-keywords nil prettify-symbols--keywords)
           (setq prettify-symbols--keywords (prettify-symbols--make-keywords))
           (font-lock-add-keywords nil prettify-symbols--keywords)
           (while (re-search-forward re nil t)
             (font-lock-flush (line-beginning-position) (line-end-position))))))

     (defun rasmus/org-prettify-src ()
       "Hide src options via `prettify-symbols-mode'.

     `prettify-symbols-mode' is used because it has uncollpasing. It's
     may not be efficient."
       (let* ((case-fold-search t)
              (at-src-block (save-excursion
                              (beginning-of-line)
                              (looking-at "^[ \t]*#\\+begin_src[ \t]+[^ \f\t\n\r\v]+[ \t]*"))))
         ;; Test if we moved out of a block.
         (when (or (and rasmus/org-at-src-begin
                        (not at-src-block))
                   ;; File was just opened.
                   (eq rasmus/org-at-src-begin -1))
           (rasmus/org-prettify-src--update))
         (setq rasmus/org-at-src-begin at-src-block)))

     (defun rasmus/org-prettify-symbols ()
       (mapc (apply-partially 'add-to-list 'prettify-symbols-alist)
             (cl-reduce 'append
                        (mapcar (lambda (x) (list x (cons (upcase (car x)) (cdr x))))
                                `(("#+begin_src" . ?✎) ;; ✎
                                  ("#+end_src"   . ?□) ;; ⏹
                                  ("#+header:" . ,rasmus/ob-header-symbol)
                                  ("#+begin_quote" . ?»)
                                  ("#+end_quote" . ?«)))))
       (turn-on-prettify-symbols-mode)
       (add-hook 'post-command-hook 'rasmus/org-prettify-src t t))
     (add-hook 'org-mode-hook #'rasmus/org-prettify-symbols))
   #+END_SRC

   org-babel 的语言设置
   #+BEGIN_SRC emacs-lisp
   (require 'ob-python)
   (org-babel-do-load-languages
    'org-babel-load-languages '((dot . t)
                                (ditaa . t)
                                (lisp . t)
                                (octave . t)
                                (gnuplot . t)
                                (python . t)
                                (C . t)
                                (shell . t)
                                (java . t)
                                (latex . t)
                                (clojure . t)
                                (ruby . t)
                                (plantuml . t)
                                (cypher . t)
                                (emacs-lisp . t))
    )
   #+end_src

** 个人管理

   设置基础的任务状态关键词，其中 "DONE" 和 "ABORT" 表示终结状态，并且用 "@" 设置为在进入终结状态时，要求输入笔记；用 "!" 设置从终结状态变化为其他状态时自动添加变更信息。
   #+BEGIN_SRC emacs-lisp
   (setq org-use-fast-todo-selection t)
   (setq org-todo-keywords '((sequence "TODO(t)" "NEXT(n)" "|" "DONE(d@/!)" "ABORT(a@/!)")))
   (setq org-log-done t)
   (setq org-log-into-drawer t)
   #+END_SRC

   任务关键词还可以在具体的文件中用 =#+SEQ_TODO: TODO NEXT | DONE= 这样的方式单独设置。

   此外要求子任务未完成时不能将父任务标记为完成
   #+BEGIN_SRC emacs-lisp
   (setq org-enforce-todo-dependencies t)
   (setq org-enforce-todo-checkbox-dependencies t)
   #+END_SRC

   然后是 org-capture 的模板，我的模板暂时分为以下几个:
   + Inbox: 用来收集基础的未归类的内容

     #+BEGIN_SRC emacs-lisp
     (setq org-capture-templates nil)
     (add-to-list 'org-capture-templates
                  '("i" "Inbox"
                    entry (file "~/Dropbox/org/inbox.org")
                    "* %U - %^{heading} %^g\n %?\n"
                    :empty-lines 1
                    ))
     #+END_SRC

   + Task: 用来记录任务

     嗯没错，时隔多年，我又要启用 task 系统了！

     为所有任务设置两个字母的 key，并共享相同的前缀
     #+BEGIN_SRC emacs-lisp
     (add-to-list 'org-capture-templates '("t" "Tasks"))
     #+END_SRC

     增加普通任务
     #+BEGIN_SRC emacs-lisp
     (add-to-list 'org-capture-templates
                  '("ti" "General Task" entry
                    (file+olp "~/Dropbox/org/task.org" "Inbox")
                    "* TODO %^{title}\nSCHEDULED: %^T DEADLINE: %^t\n\n  %?"
                    :empty-lines 1))
     #+END_SRC

     增加书籍阅读任务
     #+BEGIN_SRC emacs-lisp
     (add-to-list 'org-capture-templates
                  '("tb" "Book Reading Task" entry
                    (file+olp "~/Dropbox/org/task.org" "Reading" "Book")
                    "* TODO %^{title}\nSCHEDULED: %^T DEADLINE: %^t\n\n  %?"
                    :empty-lines 1))
     #+END_SRC

     增加论文阅读任务
     #+BEGIN_SRC emacs-lisp
     (add-to-list 'org-capture-templates
                  '("tp" "Paper Reading Task" entry
                    (file+olp "~/Dropbox/org/task.org" "Reading" "Paper")
                    "* TODO %^{title}\nSCHEDULED: %^T DEADLINE: %^t\n\n  %?"
                    :empty-lines 1))
     #+END_SRC

     增加工作任务（仅用来做任务管理，具体工作内容还是在journal.org中记录）
     #+BEGIN_SRC emacs-lisp
     (add-to-list 'org-capture-templates
                  '("tw" "Work Task" entry
                    (file+olp "~/Dropbox/org/task.org" "Work")
                    "* TODO %^{title}\nSCHEDULED: %^T DEADLINE: %^t\n\n  %?"
                    :clock-in t :clock-resume t :empty-lines 1))
     #+END_SRC

     然后，在 task.org 中增加一个 Knowledge 的章节，用来督促自己定期整理知识，形成 anki 卡片！

     增加写作任务
     #+BEGIN_SRC emacs-lisp
     (add-to-list 'org-capture-templates
                  '("tB" "Blog Writing Task" entry
                    (file+olp "~/Dropbox/org/task.org" "Knowledge" "Blog")
                    "* TODO %^{title}\nSCHEDULED: %^T DEADLINE: %^t\n\n  %?"
                    :empty-lines 1))
     #+END_SRC

   + Web: 用来收集 web 内容

     用的是 org-protocol
     #+BEGIN_SRC emacs-lisp
     (require 'org-protocol)
     #+END_SRC

     添加一个组模板吧
     #+BEGIN_SRC emacs-lisp
     (add-to-list 'org-capture-templates '("w" "Web"))
     #+END_SRC

     定义一个函数，用来将选中内容插入到同一个 headline 中
     #+BEGIN_SRC emacs-lisp
     (defun org-capture-template-goto-link ()
       (org-capture-put :target (list 'file+headline
                                      (nth 1 (org-capture-get :target))
                                      (org-capture-get :annotation)))
       (org-capture-put-target-region-and-position)
       (widen)
       (let ((heading (nth 2 (org-capture-get :target))))
         (goto-char (point-min))
         (if (re-search-forward
              (format org-complex-heading-regexp-format (regexp-quote heading)) nil t)
             (org-end-of-subtree)
           (goto-char (point-max))
           (or (bolp) (insert "\n"))
           (insert "* " heading "\n"))))
     #+END_SRC

     和 org-protocol 结合来做收集
     #+BEGIN_SRC emacs-lisp
     (add-to-list 'org-capture-templates
                  '("wa" "Web Annotation" plain
                    (file+function "~/Dropbox/org/inbox.org" org-capture-template-goto-link)
                    "  %U\n  #+BEGIN_EXAMPLE\n  %:initial\n  #+END_EXAMPLE" :empty-lines 1 :immediate-finish t))
     #+END_SRC

     利用 [[https://github.com/alphapapa/org-protocol-capture-html][org-protocol-capture-html]] 来做稍后阅读
     #+BEGIN_SRC emacs-lisp
     (add-to-list 'load-path "~/.emacs.d/site-lisp/org-protocol-capture-html")
     (require 'org-protocol-capture-html)
     #+END_SRC

     #+BEGIN_SRC emacs-lisp
     (defun web-file-to-save ()
       (concat "~/Dropbox/org/web" (org-capture-get :description) ".org"))

     (add-to-list 'org-capture-templates
                  '("wr" "Web Reading" plain
                    (function web-file-to-save)
                    "LINK: %:link\n\n%:initial" :immediate-finish t))
     #+END_SRC

     <2018-04-02 一 07:46> 嗯，似乎还没有完成，会让我选文件来着……

   + Notes: 分类明确的笔记内容

     #+BEGIN_SRC emacs-lisp
     (add-to-list 'org-capture-templates
                  '("n" "Notes"
                    entry (file "~/Dropbox/org/notes/inbox.org")
                    "* %^{heading} %t %^g\n  %?\n"
                    :empty-lines 1
                    ))
     #+END_SRC

   + Journal: 日志

     #+BEGIN_SRC emacs-lisp
     (add-to-list 'org-capture-templates
                  '("j" "Journal"
                    entry (file+datetree "~/Dropbox/org/journal.org")
                    "* %U - %^{heading} %^g\n %?\n"
                    :empty-lines 1
                    ))
     #+END_SRC

   + Card

     为所有卡片设置两个字母的 key，并共享相同的前缀
     #+BEGIN_SRC emacs-lisp
     (add-to-list 'org-capture-templates
                  '("c" "Cards"))
     #+END_SRC

     词汇卡，需要在 anki 中建好 Vocabulary 这个记忆库，使用 words 这个笔记类型。
     #+BEGIN_SRC emacs-lisp
     (add-to-list 'org-capture-templates
                  `("cv" "Vocabulary"
                    entry (file+headline "~/Dropbox/org/cards.org" "Vocabulary")
                    ,(concat "* %^{heading|Item} :note\n"
                           "  :PROPERTIES:\n"
                           "  :ANKI_DECK: Vocabulary\n"
                           "  :ANKI_NOTE_TYPE: words\n"
                           "  :END:\n\n"
                           "** 单词\n\n   %?\n** 释义\n\n** 例句\n\n** 音标\n")
                   :empty-lines 1
                   ))
     #+END_SRC

     人名卡，需要在 anki 中建好 Person 这个记忆库，使用 person 这个笔记类型。
     #+BEGIN_SRC emacs-lisp
     (add-to-list 'org-capture-templates
                  `("cp" "Person"
                    entry (file+headline "~/Dropbox/org/cards.org" "Person")
                    ,(concat "* %^{heading|Item} :note\n"
                             "  :PROPERTIES:\n"
                             "  :ANKI_DECK: Person\n"
                             "  :ANKI_NOTE_TYPE: person\n"
                             "  :END:\n\n"
                             "** 名字\n\n   %?\n** 生卒年\n\n** 主要事迹\n")
                    :empty-lines 1
                    ))
     #+END_SRC

     事实卡，需要在 anki 中建好 Fact 这个记忆库，使用 fact 这个笔记类型。
     #+BEGIN_SRC emacs-lisp
     (add-to-list 'org-capture-templates
                  `("cf" "Fact"
                    entry (file+headline "~/Dropbox/org/cards.org" "Fact")
                    ,(concat "* %^{heading|Item} :note\n"
                             "  :PROPERTIES:\n"
                             "  :ANKI_DECK: Fact\n"
                             "  :ANKI_NOTE_TYPE: fact\n"
                             "  :END:\n\n"
                             "** 文字\n\n   %?")
                    :empty-lines 1
                    ))
     #+END_SRC

   在上述模板中有时候需要填 tag，我把 tag 的举例设置得大一些，这样当标题比较长的时候不会影响阅读
   #+BEGIN_SRC emacs-lisp
   (setq org-tags-column -100)
   #+END_SRC

   Org Brain 设置
   #+BEGIN_SRC emacs-lisp
   (setq org-brain-path "~/Dropbox/org/brain")
   #+END_SRC

   然后是 agenda 相关的设置
   #+BEGIN_SRC emacs-lisp
   ;; (setq org-agenda-ndays 1)
   ;; org-agenda-ndays 自 org 7.8 后就失效了
   (setq org-agenda-span 'day)
   (setq org-agenda-files (list "~/Dropbox/org/task.org"))
   #+END_SRC

   clock 相关的设置
   #+BEGIN_SRC emacs-lisp
   (setq org-clock-into-drawer t)
   #+END_SRC

   2017/01/31 MobileOrg 有了更新，界面、操作都变得好多了，因此还是把 MobileOrg 用起来。

   前面已经设置了 agenda-files，它们会被同步到手机上，不用再额外设置。除此以外设置一下用来接收手机上内容的文件，照例放到 inbox 里去。
   #+BEGIN_SRC emacs-lisp
   (require 'org-mobile)
   (setq org-mobile-directory "~/Dropbox/应用/MobileOrg")
   (setq org-mobile-inbox-for-pull "~/Dropbox/org/inbox.org")
   #+END_SRC

   为方便清理和回顾，让各个任务能在各文件之间转接
   #+BEGIN_SRC emacs-lisp
   (setq org-refile-use-outline-path 'file)
   (setq org-refile-targets
         '(("~/Dropbox/org/inbox.org" :level . 1)
           ("~/Dropbox/org/journal.org" :level . 1)
           ("~/Dropbox/org/memo.org" :level . 1)
           ("~/Dropbox/org/notes/inbox.org" :level . 1)
           ("~/Dropbox/org/notes/nlp.org" :level . 1)
           ("~/Dropbox/org/notes/linux.org" :level . 1)
           ("~/Dropbox/org/notes/emacs.org" :level . 1)
           ("~/Dropbox/org/notes/math.org" :level . 1)
           ("~/Dropbox/org/notes/work.org" :level . 1)
           ("~/Dropbox/org/notes/latex.org" :level . 1)
           ("~/Dropbox/org/notes/rss.org" :level . 1)
           ("~/Dropbox/org/notes/economics.org" :level . 1)
           ("~/Dropbox/org/task.org" :level . 2)
           ("~/Dropbox/org/notes/machine-learning.org" :level . 1)
           ("~/Dropbox/org/notes/python.org" :level . 1)))
   #+END_SRC

   设置 helm-org-rifle
   #+BEGIN_SRC emacs-lisp
   ;; 在搜索结果中显示 heading 的全路径(包括文件名)
   (setq helm-org-rifle-show-path t)
   #+END_SRC

** 写作设置

   #+BEGIN_SRC emacs-lisp
   (defvar post-dir "~/Dropbox/org/blog/_posts/")
   (defun blog-post (title)
     (interactive "sEnter title: ")
     (let ((post-file (concat post-dir
                              (format-time-string "%Y-%m-%d")
                              "-"
                              title
                              ".org")))
       (progn
         (switch-to-buffer (find-file-noselect post-file))
         (insert (concat "#+startup: showall\n"
                         "#+options: toc:nil\n"
                         "#+begin_export html\n"
                         "---\n"
                         "layout     : post\n"
                         "title      : \n"
                         "categories : \n"
                         "tags       : \n"
                         "---\n"
                         "#+end_export\n"
                         "#+TOC: headlines 2\n")))
       ))
   (defun publish-project (project no-cache)
     (interactive "sName of project: \nsNo-cache?[y/n] ")
     (if (or (string= no-cache "y")
             (string= no-cache "Y"))
         (setq org-publish-use-timestamps-flag nil))
     (org-publish-project project)
     (setq org-publish-use-timestamps-flag t))

      ;;;; PUBLISH(org)
   (setq org-export-default-language "zh-CN")
   (setq org-publish-project-alist
         '(("blog-org"
            ;; Path to your org files.
            :base-directory "~/Dropbox/org/blog/"
            :base-extension "org"
            ;; Path to your Jekyll project.
            :publishing-directory "~/Projects/github-pages/"
            :recursive t
            :htmlized-source t
            :section-numbers nil
            :publishing-function org-html-publish-to-html
            :headline-levels 4
            :html-extension "html"
            :body-only t; Only export section between <body> </body>
            :table-of-contents nil
            )
           ("blog-static"
            :base-directory "~/Dropbox/org/blog/"
            :base-extension "css\\|js\\|png\\|jpg\\|gif\\|pdf\\|mp3\\|ogg\\|swf\\|php"
            :publishing-directory "~/Projects/github-pages/"
            :recursive t
            :publishing-function org-publish-attachment
            )
           ("blog" :components ("blog-org" "blog-static"))))

   #+END_SRC

* 其他

** Edit Serevr

   配合 Chrome 的 Edit with Emacs 插件可以在需要输入、编辑时调用 Emacs
   #+BEGIN_SRC emacs-lisp
   (when (require 'edit-server nil t)
     (setq edit-server-new-frame nil)
     (edit-server-start))

   (add-hook 'edit-server-start-hook
             (lambda ()
               (when (or (string-match "github.com" (buffer-name))
                         (string-match "gitlab.com" (buffer-name))
                         (string-match "bearychat.com" (buffer-name)))
                 (markdown-mode))))
   #+END_SRC

* 扩展无关的函数定义

** 对注释和反注释的改进

   默认的注释行为是按下 'M-;' 后对所在行进行注释，但经常还会需要 *将某行注释掉* ，这里定义了一个为某行添加注释或者注释某行的方法。
   #+BEGIN_SRC emacs-lisp
   (defun comment-dwim-line (&optional arg)
     (interactive "*P")
     (comment-normalize-vars)
     (if (and
          (not (region-active-p))
          (not (looking-at "[ \t]*$")))
         (comment-or-uncomment-region
          (line-beginning-position)
          (line-end-position))
       (comment-dwim arg)))
   #+END_SRC

** 行/区域上下移动

   首先定义行移动的方法
   #+BEGIN_SRC emacs-lisp
   (defun move-line (n)
     "Move the current line up or down by N lines."
     (interactive "p")
     (setq col (current-column))
     (beginning-of-line)
     (setq start (point))
     (end-of-line)
     (forward-char)
     (setq end (point))
     (let ((line-text (delete-and-extract-region start end)))
       (forward-line n)
       (insert line-text)
       (forward-line -1)
       (forward-char col)))

   (defun move-line-up (n)
     "Move the current line up by N lines."
     (interactive "p")
     (move-line (if (null n) -1 (- n))))

   (defun move-line-down (n)
     "Move the current line down by N lines."
     (interactive "p")
     (move-line (if (null n) 1 n)))
   #+END_SRC

   然后定义区域移动的方法
   #+BEGIN_SRC emacs-lisp
   (defun move-region (start end n)
     "Move the current region up or down by N lines."
     (interactive "r\np")
     (let ((line-text (delete-and-extract-region start end)))
       (forward-line n)
       (let ((start (point)))
         (insert line-text)
         (setq deactivate-mark nil)
         (set-mark start))))

   (defun move-region-up (start end n)
     "Move the current line up by N lines."
     (interactive "r\np")
     (move-region start end (if (null n) -1 (- n))))

   (defun move-region-down (start end n)
     "Move the current line down by N lines."
     (interactive "r\np")
     (move-region start end (if (null n) 1 n)))
   #+END_SRC

   最后将行移动和区域移动整合到一起，这样在后面定义快捷键的时候可以使用同一个快捷键
   #+BEGIN_SRC emacs-lisp
   (defun move-line-region-up (&optional start end n)
     (interactive "r\np")
     (if (use-region-p) (move-region-up start end n) (move-line-up n)))

   (defun move-line-region-down (&optional start end n)
     (interactive "r\np")
     (if (use-region-p) (move-region-down start end n) (move-line-down n)));
   #+END_SRC

** 显示非 ASCII 字符

   执行这个方法后，能在一个新的 buffer 中高亮所有非 ASCII 字符。写这个方法的一个目的是用来检查文本中是否有全角空白字符。
   #+BEGIN_SRC emacs-lisp
   (defun occur-non-ascii ()
     "Find any non-ascii characters in the current buffer."
     (interactive)
     (occur "[^[:ascii:]]"))
   #+END_SRC

** 插入 uuid

   #+BEGIN_SRC emacs-lisp
   (defun insert-random-uuid ()
     "Insert a UUID. This uses a simple hashing of variable data.
   Example of a UUID: 1df63142-a513-c850-31a3-535fc3520c3d

   Note: this code uses https://en.wikipedia.org/wiki/Md5 , which is not cryptographically safe. I'm not sure what's the implication of its use here.

   Version 2015-01-30
   URL `http://ergoemacs.org/emacs/elisp_generate_uuid.html'
   "
     ;; by Christopher Wellons, 2011-11-18. Editted by Xah Lee.
     ;; Edited by Hideki Saito further to generate all valid variants for "N" in xxxxxxxx-xxxx-Mxxx-Nxxx-xxxxxxxxxxxx format.
     (interactive)
     (let ((myStr (md5 (format "%s%s%s%s%s%s%s%s%s%s"
                               (user-uid)
                               (emacs-pid)
                               (system-name)
                               (user-full-name)
                               (current-time)
                               (emacs-uptime)
                               (garbage-collect)
                               (buffer-string)
                               (random)
                               (recent-keys)))))

       (insert (format "%s-%s-4%s-%s%s-%s"
                       (substring myStr 0 8)
                       (substring myStr 8 12)
                       (substring myStr 13 16)
                       (format "%x" (+ 8 (random 4)))
                       (substring myStr 17 20)
                       (substring myStr 20 32)))))

   #+END_SRC

* 全局快捷键设置

  首先设置 smex，有了 smex 后，可以减少快捷键的使用
  #+BEGIN_SRC emacs-lisp
  (global-set-key (kbd "C-c C-c M-x") 'execute-extended-command)
  (global-set-key (kbd "C-x k") 'kill-this-buffer)
  #+END_SRC

  为了更好地使用 smex，为一些常用的命令设置别名。比如说下面这些命令，原先都设置为用 F2、F3 这样的键，在 HHKB 键盘上很不方便。
  #+BEGIN_SRC emacs-lisp
  (defalias 'z/hs 'hs-toggle-hiding)
  (defalias 'z/bms 'bookmark-set)
  (defalias 'z/bml 'bookmark-bmenu-list)
  (defalias 'z/fs 'toggle-frame-fullscreen)
  (defalias 'z/mw 'toggle-frame-maximized)
  (defalias 'z/evalb 'eval-buffer)
  (defalias 'z/evals 'eval-last-sexp)
  (defalias 'z/latex 'org-preview-latex-fragment)
  (defalias 'z/image 'org-toggle-inline-images)
  (defalias 'z/gs 'magit-status)
  (defalias 'z/cm 'magit-commit)
  (defalias 'z/rebase 'magit-rebase-interactive)
  (defalias 'z/app 'counsel-linux-app)
  (defalias 'z/repl 'replace-string)
  (defalias 'z/repr 'replace-regexp)
  (defalias 'z/install 'package-install)
  #+END_SRC

  用于编辑的一些快捷键
  #+BEGIN_SRC emacs-lisp
  (global-set-key (kbd "M-p") 'move-line-region-up)
  (global-set-key (kbd "M-n") 'move-line-region-down)
  (global-set-key (kbd "C-z") 'delete-trailing-whitespace)
  #+END_SRC

  其他全局快捷键
  #+BEGIN_SRC emacs-lisp
  ;; 选择
  (global-set-key (kbd "C-M-SPC") 'set-mark-command)
  (global-set-key (kbd "C-=") 'er/expand-region)

  ;; 搜索，快速定位
  (global-set-key (kbd "C-c j") 'avy-goto-word-or-subword-1)
  (global-set-key (kbd "C-c h o") 'helm-org-rifle)  ;搜索 org 文件
  (global-set-key (kbd "C-c h s") 'helm-do-grep-ag) ;使用 ag 搜索目录
  (global-set-key (kbd "C-s") 'swiper)              ;不使用内置的 search 而是使用 swiper
  (setq ivy-display-style 'fancy)

  ;; coding
  (global-set-key (kbd "M-;") 'comment-dwim-line)

  ;; org-mode
  (global-set-key (kbd "C-c b") 'org-iswitchb)
  (global-set-key (kbd "C-c c") 'org-capture)
  (global-set-key (kbd "C-c l") 'org-store-link)
  (global-set-key (kbd "C-c m") 'org-tags-view)
  (global-set-key (kbd "C-c p") 'blog-post)
  (global-set-key (kbd "C-c q") 'publish-project)
  #+END_SRC
